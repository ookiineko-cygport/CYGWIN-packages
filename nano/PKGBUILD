# Maintainer: Ookiineko <chiisaineko@protonmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

pkgname=nano
pkgver=7.0
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('i686' 'x86_64')
license=('GPL')
url="https://www.nano-editor.org"
groups=('editors')
backup=('etc/nanorc')
source=(https://www.nano-editor.org/dist/v7/${pkgname}-${pkgver}.tar.xz)
sha256sums=('8dd6eac38b2b8786d82681f0e1afd84f6b75210d17391b6443c437e451552149')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  autoreconf -fiv
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-color \
    --enable-nanorc \
    --enable-utf8

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/sample.nanorc ${pkgdir}/etc/nanorc
}
