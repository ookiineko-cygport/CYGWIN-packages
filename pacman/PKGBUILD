# Maintainer: Ookiineko <chiisaineko@protonmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=pacman
pkgver=6.0.1
pkgrel=25
pkgdesc="A library-based package manager with dependency support (CYGWIN port)"
arch=('i686' 'x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
backup=("etc/pacman.conf"
        "etc/makepkg.conf")
_commit="0a6fecd07271a54d9009ea7204c0e6288a44212b"
source=(pacman-${pkgver}::git+https://gitlab.archlinux.org/pacman/pacman.git#commit=${_commit}
        0001-Msysize.patch
        0002-More-debugging-info.patch
        0003-Core-update.patch
        0004-Remove-ldconfig.patch
        0005-Change-the-epoch-separator.patch
        0006-pacman-libalpm-ignore-file-conflicts-for-foo.exe-foo.patch
        0007-Use-pipe-instead-of-socket.patch
        0008-Translate-CYGWIN-specific-messages-into-Japanese.patch
        0009-Fix-asciidoc-argparse.patch
        0010-Remove-fakeroot.patch
        0011-Remove-sudo.patch
        0012-Use-msys-tools.patch
        0013-Export-CC-and-CXX-variables-explicitly.patch
        0014-pacman.c-handle-cr-on-stdin-as-well.patch
        0015-Strip-static-libraries-as-well.patch
        0016-Fix-building-staticlibs-with-LTO.patch
        0017-fixup-Export-CC-and-CXX-variables-explicitly.patch
        0018-Make-lint_pkgbuild-opt-in-via-MAKEPKG_LINT_PKGBUILD-.patch
        0019-libmakepkg-fix-compatibility-with-bash-5.2-globskipd.patch
        0020-Cygwinize.patch
        0021-Replace-use-of-the-GPGME_KEYLIST_MODE_LOCATE-alias.patch
        0022-conf-remove-glibc.patch)
sha256sums=('SKIP'
            '87b6965a33b0cd3c081f63618c306d71eea62213e2557eb465b09ac239e6a607'
            '32a23ef1e3f609df18e8b8c67c2562dfe0186fd92877cb26b4fad3c6c972550e'
            'c3f7633bb953cee24869be06cea223c3115e37ab285e84bab81d9d9c5abb82fc'
            '2a31dbed5f627601f8c2f5e585fa4710dab01d996c85e19fd59179b697382cd1'
            'b4a193eaf9f6f889e1047159c30784cd4eb89b47b4a45554eaae7a3755e0c57c'
            '9926f23fa84ae222a8e9da83dd037c2a93916a6c8184ff56a5ace69bea2d9cf1'
            'cacc4b236b09aa7551ddda86e29053252f87550d0a1681db20dcd38eb6497cdf'
            'e8fd54f916db2e8d9254d2951ca3df5054e520ea82e7965180ed1999262b5859'
            '9fcae9f1418a9fe9b0ef10c11b5535199265c3b0d455d7fe6d9349229b5c153d'
            'e279fbfc99eb606fd738c2e3c457c57d10f92d019ce990c401626736295d18cc'
            '9cf619322392b061f840e8a7d3cb99a36e42834bc1ce1211f1625dcfe6f17aeb'
            '784762909bc53609eb5b0681b74bf36542e4ae2314b5ec9dc35bb0e5f94fcd6a'
            '08604500a8ff5b19e81934f0c7d44dd97c45be65c4a72688c7462c77607d58da'
            '729ecf8560d95bb6f3356c946b80c3994cc94d2cecb4c1eb09f2c876066a3a6b'
            '38229ca9e7d340cdea98b5846a7df64edfbbf6247304d8241dac503575fb2e6c'
            'e116763437a81cde0333a87de90432f1483d93d8de9b0aa2b0541beb0f3e92ae'
            '4706e852bd42102e69fa7228ca383008539fbdad5425dcc084dd48a502da4676'
            'bdc3fbf66c25c1afdffd23b9c9fdc9d237236a62ec3ed8cb7510cd7dabab13ea'
            '32cc157391e63c7a9c1abdd48b435627b12376d204ee8cbe2c406795ffffbc3b'
            'd39b7e30994b4eeaa7ee09cd3785f7dce411fbdd1214f4b3d1e31a498d7e1260'
            '147cf0fc027a2cfb825e7921c0f591cb0f1a96f90cf88c55a05e351ec159fed1'
            'f07a8fab73b4bae523a9368b253d887727584b103a6309d5a5c80b12bcef45b6')

apply_git_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

 apply_git_with_msg 0001-Msysize.patch \
  0002-More-debugging-info.patch \
  0003-Core-update.patch \
  0004-Remove-ldconfig.patch \
  0005-Change-the-epoch-separator.patch \
  0006-pacman-libalpm-ignore-file-conflicts-for-foo.exe-foo.patch \
  0007-Use-pipe-instead-of-socket.patch \
  0008-Translate-CYGWIN-specific-messages-into-Japanese.patch \
  0009-Fix-asciidoc-argparse.patch \
  0010-Remove-fakeroot.patch \
  0011-Remove-sudo.patch \
  0012-Use-msys-tools.patch \
  0013-Export-CC-and-CXX-variables-explicitly.patch \
  0014-pacman.c-handle-cr-on-stdin-as-well.patch \
  0015-Strip-static-libraries-as-well.patch \
  0016-Fix-building-staticlibs-with-LTO.patch \
  0017-fixup-Export-CC-and-CXX-variables-explicitly.patch \
  0018-Make-lint_pkgbuild-opt-in-via-MAKEPKG_LINT_PKGBUILD-.patch \
  0019-libmakepkg-fix-compatibility-with-bash-5.2-globskipd.patch \
  0020-Cygwinize.patch \
  0021-Replace-use-of-the-GPGME_KEYLIST_MODE_LOCATE-alias.patch \
  0022-conf-remove-glibc.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  LDFLAGS+=" -static-libgcc" \
  meson build-${CARCH} \
    --buildtype=plain \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --default-library=static \
    -Dbuildstatic=true \
    -Ddoc=enabled \
    -Ddoxygen=enabled \
    -Dgpgme=enabled \
    -Dcurl=enabled \
    -Duse-git-version=false \
    -Dpkg-ext=.pkg.tar.xz \
    -Dscriptlet-shell=/usr/bin/bash

  meson compile -C build-${CARCH}
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  meson test -C build-${CARCH}
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  DESTDIR=${pkgdir} meson install -C build-${CARCH}

  mycarch="x86_64"
  mychost="x86_64-pc-cygwin"
  myflags="-march=x86-64"

  # set things correctly in the default conf file
  sed -i ${pkgdir}/etc/makepkg.conf \
    -e "s|@CARCH[@]|${mycarch}|g" \
    -e "s|@CHOST[@]|${mychost}|g" \
    -e "s|@CARCHFLAGS[@]|${myflags}|g"

  # correct mode
  chmod 775 `find ${pkgdir}/usr/share/zsh -type d`
}
