# Maintainer: Ookiineko <chiisaineko@protonmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

pkgname=screenfetch
_pkgname=screenFetch
pkgver=3.9.1
pkgrel=2
pkgdesc="Bash screenshot information tool"
arch=('any')
url="https://github.com/KittyKatt/screenFetch"
license=('GPL')
#optdepends=('scrot: screenshot taking'
#            'curl: screenshot uploading')
_commit="fbb5ea90a037b7d5aed1951e1a1fcd180ca137d5"
source=("$_pkgname"::"git+https://github.com/KittyKatt/${_pkgname}.git#commit=${_commit}")
sha256sums=('SKIP')

build() {
  cd "${srcdir}/${_pkgname}"
  ./update-manpage.sh
}

package() {
  cd "${srcdir}/${_pkgname}"
  install -Dm755 screenfetch-dev "${pkgdir}/usr/bin/screenfetch"
  install -Dm644 screenfetch.1 "${pkgdir}/usr/share/man/man1/screenfetch.1"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -Dm644 CHANGELOG "${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG"
  install -Dm644 README.mkdn "${pkgdir}/usr/share/doc/${pkgname}/README.mkdn"
  install -Dm644 TODO "${pkgdir}/usr/share/doc/${pkgname}/TODO"
}
