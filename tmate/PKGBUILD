pkgname=tmate
pkgver=2.4.0
pkgrel=2
pkgdesc="Instant Terminal Sharing http://tmate.io/"
arch=('i686' 'x86_64')
license=('ISC')
url="http://tmate.io/"
depends=('msgpack-c')
makedepends=('msgpack-c-devel')

source=("$pkgname"::"git+https://github.com/tmate-io/tmate.git#tag=$pkgver"
        "0004-Avoid-unsupported-GCC-option.patch"
        "0005-Try-to-side-step-libevent-for-DNS-resolution.patch")
sha256sums=('SKIP'
            'b2d916cb542cdd6e70f4864c8eda4fcf2d16c3d3573990f3fa3b3e2200c74a5f'
            'c60a65e8a2dfc37fb58d661cfeb211dc529bef7a89dc09198e1da9d0e0faecac')

prepare() {
  cd ${srcdir}/$pkgname
  patch -p1 -i ${srcdir}/0004-Avoid-unsupported-GCC-option.patch
  patch -p1 -i ${srcdir}/0005-Try-to-side-step-libevent-for-DNS-resolution.patch
  ./autogen.sh
}

build() {
  cd ${srcdir}/$pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    CPPFLAGS="${CPPFLAGS} -I/usr/include/ncursesw -D_GNU_SOURCE=1"
  make
}

package() {
  cd ${srcdir}/$pkgname
  make DESTDIR=${pkgdir} install
}
